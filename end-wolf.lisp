(uiop:define-package #:end-wolf
  (:use #:cl #:iterate #:alexandria))

(in-package #:end-wolf)

(declaim (type (simple-array (unsigned-byte 10) (18 4)) *binomial-table*)
         (inline encode-set decode-set
                 reencode-wolf-index decode-wolf-index max-wolf-index))

(deftype state () '(unsigned-byte 35))
(deftype board () '(unsigned-byte 25))
(deftype bitpos () '(unsigned-byte 5))

(sb-ext:defglobal *binomial-table*
    (let ((table (make-array '(18 4) :initial-element 0 :element-type '(unsigned-byte 10))))
      (loop for n from 0 to 17 do
        (setf (aref table n 0) 1)
        (loop for k from 1 to (min n 3) do
          (setf (aref table n k)
                (+ (aref table (1- n) (1- k))
                   (aref table (1- n) k)))))
      table))

(defun encode-set (a b c)
  (declare (optimize speed) (type (unsigned-byte 5) a b c))
  (+ a (aref *binomial-table* b 2) (aref *binomial-table* c 3)))

(defun decode-set (index)
  (declare (optimize speed) (type (unsigned-byte 10) index))
  (let* ((n3 (loop for n from 17 downto 0
                   if (<= (aref *binomial-table* n 3) index) return n))
         (rem3 (- index (aref *binomial-table* n3 3)))

         (n2 (loop for n from (1- n3) downto 0
                   if (<= (aref *binomial-table* n 2) rem3) return n))
         (rem2 (- rem3 (aref *binomial-table* n2 2)))

         (n1 (loop for n from (1- n2) downto 0
                   if (<= n rem2) return n)))
    (values n1 n2 n3)))

(defun reencode-wolf-index (mask state)
  (declare (optimize speed)
           (type board mask)
           (type state state))
  (let ((a 0) (b 0) (c 0))
    (declare (type bitpos a b c))
    (let* ((low-bit (logand mask (- mask)))
           (idx (logcount (logand state (1- low-bit)))))
      (setq a idx mask (logxor mask low-bit)))
    (let* ((low-bit (logand mask (- mask)))
           (idx (logcount (logand state (1- low-bit)))))
      (setq b idx mask (logxor mask low-bit)))
    (let* ((low-bit (logand mask (- mask)))
           (idx (logcount (logand state (1- low-bit)))))
      (setq c idx mask (logxor mask low-bit)))
    (setf (ldb (byte 10 25) state) (encode-set a b c))
    state))

(defun decode-wolf-index (state)
  (declare (optimize speed)
           (type state state))
  (multiple-value-bind (a b c) (decode-set (ldb (byte 10 25) state))
    (let ((mask 0) (count 0))
      (declare (type board mask)
               (type bitpos count))
      (flet ((find-next (target)
               (loop while (< count target) do
                 (setf state (logand state (1- state))) ; Clear lowest bit
                 (incf count))
               (let ((low-bit (logand state (- state))))
                 (setf mask (logior mask low-bit)))))
        (find-next a)
        (find-next b)
        (find-next c)
        mask))))

;; inclusive
(defun max-wolf-index (max-n-pieces)
  (encode-set (- max-n-pieces 3) (- max-n-pieces 2) (- max-n-pieces 1)))

(defun encode-state (list)
  (let ((state 0)
        (logcount 0)
        (ws nil)
        (i 0))
    (dolist (row list)
      (dolist (piece row)
        (ecase piece
          (w (push logcount ws)
           (setf (ldb (byte 1 i) state) 1)
           (incf logcount))
          (s (setf (ldb (byte 1 i) state) 1)
           (incf logcount))
          (-))
        (incf i)))
    (setf (ldb (byte 10 25) state) (apply #'encode-set (reverse ws)))
    state))

(defun decode-state (state)
  (let ((wm (decode-wolf-index state)))
    (iter (with bitpos = 0)
      (for i below 5)
      (collecting
        (iter (for j below 5)
          (if (plusp (ldb (byte 1 bitpos) state))
              (collect (if (plusp (ldb (byte 1 bitpos) wm))
                           'w 's))
              (collect '-))
          (incf bitpos))))))

(defun print-state (state)
  (format t "~{~{~a~^ ~}~%~}~%"(decode-state state)))

(defun fuzz-encode-decode (seed)
  (let ((*random-state* (sb-ext:seed-random-state seed)))
    (iter (with n = 0)
      (repeat 100000)
      (for wc = 0)
      (for sc = 0)
      (for state = (iter
                     (for i below 5)
                     (collecting
                       (iter (for j below 5)
                         (collecting
                           (case (random 5)
                             ((0 1) '-)
                             ((2 3) (if (< sc 15)
                                        (prog1 's (incf sc))
                                        '-))
                             (4 (if (< wc 3)
                                    (prog1 'w (incf wc))
                                    '-))))))))
      (unless (< wc 3)
        (incf n)
        (let ((encoded (encode-state state)))
          (unless (equal state (decode-state encoded))
            (error "state ~a" state))
          (unless (= encoded (reencode-wolf-index (decode-wolf-index encoded) encoded))
            (error "wolf index ~a" state))))
      (finally (return n)))))

(declaim (inline sheep-forward wolf-forward
                 sheep-backward wolf-move-backward))

(defun sheep-forward (state cont)
  (declare (optimize speed)
           (type state state)
           (type function cont))
  (let* ((wm (decode-wolf-index state))
         (sm (ldb (byte 25 0) (logxor wm state)))
         (em (ldb (byte 25 0) (lognot state))))
    (declare (type board wm sm em))
    (let* ((sm-right (ash sm 1))
           (sm-into (logand sm-right em #b1111011110111101111011110)))
      (loop while (plusp sm-into) do
        (let ((low-bit (logand sm-into (- sm-into))))
          (setf sm-into (logand sm-into (1- sm-into)))
          (funcall cont (logxor state low-bit (ash low-bit -1))))))
    (let* ((sm-left (ash sm -1))
           (sm-into (logand sm-left em #b0111101111011110111101111)))
      (loop while (plusp sm-into) do
        (let ((low-bit (logand sm-into (- sm-into))))
          (setf sm-into (logand sm-into (1- sm-into)))
          (funcall cont (logxor state low-bit (ash low-bit 1))))))
    (let* ((sm-down (ash sm 5))
           (sm-into (logand sm-down em #b1111111111111111111100000)))
      (loop while (plusp sm-into) do
        (let ((low-bit (logand sm-into (- sm-into))))
          (setf sm-into (logand sm-into (1- sm-into)))
          (funcall cont (reencode-wolf-index wm (logxor state low-bit (ash low-bit -5)))))))
    (let* ((sm-down (ash sm -5))
           (sm-into (logand sm-down em #b0000011111111111111111111)))
      (loop while (plusp sm-into) do
        (let ((low-bit (logand sm-into (- sm-into))))
          (setf sm-into (logand sm-into (1- sm-into)))
          (funcall cont (reencode-wolf-index wm (logxor state low-bit (ash low-bit 5)))))))))

(defun sheep-backward (state cont)
  (sheep-forward state cont))

(defun wolf-forward (state cont)
  (declare (optimize speed)
           (type state state)
           (type function cont))
  (let ((wm (decode-wolf-index state)))
    (macrolet ((consider-move (skip-move-reencode move-test move-pos cap-test cap-pos)
                 `(when ,move-test
                    (when (zerop (ldb (byte 1 ,move-pos) state))
                      (let ((new-state new-state))
                        (setf (ldb (byte 1 ,move-pos) new-state) 1)
                        ,(if skip-move-reencode
                             '(funcall cont new-state)
                             `(let ((new-wm new-wm))
                                (setf (ldb (byte 1 ,move-pos) new-wm) 1)
                                (funcall cont (reencode-wolf-index new-wm new-state)))))
                      (when ,cap-test
                        (when (and (plusp (ldb (byte 1 ,cap-pos) state))
                                   (zerop (ldb (byte 1 ,cap-pos) wm)))
                          (let ((new-wm new-wm))
                            (setf (ldb (byte 1 ,cap-pos) new-wm) 1)
                            (funcall cont (reencode-wolf-index new-wm new-state)))))))))
      (let ((next-wm wm))
        (iter (repeat 3)
          (let ((new-state state)
                (new-wm wm)
                (bitpos (1- (integer-length next-wm))))
            (declare (type bitpos bitpos))
            (setf (ldb (byte 1 bitpos) new-state) 0
                  (ldb (byte 1 bitpos) new-wm) 0
                  (ldb (byte 1 bitpos) next-wm) 0)
            (multiple-value-bind (i j) (floor bitpos 5)
              (consider-move t (> j 0) (- bitpos 1) (> j 1) (- bitpos 2))
              (consider-move t (< j 4) (+ bitpos 1) (< j 3) (+ bitpos 2))
              (consider-move nil (> i 0) (- bitpos 5) (> i 1) (- bitpos 10))
              (consider-move nil (< i 4) (+ bitpos 5) (< i 3) (+ bitpos 10)))))))))

(defun wolf-move-backward (state cont)
  (declare (optimize speed)
           (type state state)
           (type function cont))
  (let ((wm (decode-wolf-index state)))
    (macrolet ((consider-move (skip-move-reencode move-test move-pos)
                 `(when ,move-test
                    (when (zerop (ldb (byte 1 ,move-pos) state))
                      (let ((new-state new-state))
                        (setf (ldb (byte 1 ,move-pos) new-state) 1)
                        ,(if skip-move-reencode
                             '(funcall cont new-state)
                             `(let ((new-wm new-wm))
                                (setf (ldb (byte 1 ,move-pos) new-wm) 1)
                                (funcall cont (reencode-wolf-index new-wm new-state)))))))))
      (let ((next-wm wm))
        (iter (repeat 3)
          (let ((new-state state)
                (new-wm wm)
                (bitpos (1- (integer-length next-wm))))
            (declare (type bitpos bitpos))
            (setf (ldb (byte 1 bitpos) new-state) 0
                  (ldb (byte 1 bitpos) new-wm) 0
                  (ldb (byte 1 bitpos) next-wm) 0)
            (multiple-value-bind (i j) (floor bitpos 5)
              (consider-move t (> j 0) (- bitpos 1))
              (consider-move t (< j 4) (+ bitpos 1))
              (consider-move nil (> i 0) (- bitpos 5))
              (consider-move nil (< i 4) (+ bitpos 5)))))))))

(defconstant +unknown+ 0)
(defconstant +wolf+ 1)
(defconstant +sheep+ 2)

(deftype fate () '(unsigned-byte 2))

;; non inclusive
(defun max-state (max-n-pieces)
  (ash (1+ (max-wolf-index max-n-pieces)) 25))

(deftype tablebase () `(simple-array (unsigned-byte 8) (,(ash (max-state 17) -2))))

(defmacro bref (tablebase state)
  `(ldb (byte 2 (ash (ldb (byte 2 0) ,state) 1)) (aref ,tablebase (ash ,state -2))))

(setq *print-length* 1000)

(defmacro do-states ((state-var max-n-piece) (&rest bindings) &body body)
  `(let ((results nil)
         (n-seg (/ (max-state ,max-n-piece) 256))
         (channel (lparallel:make-channel)))
     (declare (state n-seg))
     (dotimes (i 256)
       (lparallel:submit-task
        channel
        (lambda (start)
          (declare (state start))
          (let ,bindings
            (dotimes (i n-seg)
              (let ((,state-var (+ start i)))
                (declare (state i ,state-var))
                ,@body))
            (or (list ,@(mapcar #'car bindings)) t)))
        (* i n-seg)))
     (unwind-protect
          (iter (for last-progress = (length results))
            (while (< last-progress 256))
            (sleep 1)
            (iter (for r = (lparallel:try-receive-result channel))
              (while r)
              (push r results))
            (unless (= last-progress (length results))
              (format t "~,2f% " (/ (* 100 (length results)) 256)))
            (finally (return results)))
       (lparallel:kill-tasks :default))))

(defun init-tablebase (tablebase)
  (format t "~&Init.~%")
  (do-states (state 17) ()
    (declare (optimize speed)
             (type tablebase tablebase))
    (let ((n-pieces (logcount (ldb (byte 25 0) state))))
      (when (= n-pieces 6)
        (when (<= (ldb (byte 10 25) state) (max-wolf-index 6))
          (setf (bref tablebase state) +wolf+))))))

(defun tablebase-summary (tablebase &optional (max-n-piece 17))
  (check-type tablebase tablebase)
  (sb-thread:barrier (:memory))
  (let* ((results
           (do-states (state max-n-piece)
               ((total 0)
                (sheep 0)
                (wolf 0)
                (unknown 0))
             (declare (optimize speed)
                      (type tablebase tablebase)
                      (type bitpos max-n-piece)
                      (type fixnum total sheep wolf unknown))
             (let ((n-pieces (logcount (ldb (byte 25 0) state))))
               (when (<= 6 n-pieces max-n-piece)
                 (when (<= (ldb (byte 10 25) state) (max-wolf-index n-pieces))
                   (case (bref tablebase state)
                     (#.+unknown+ (incf total) (incf unknown))
                     (#.+wolf+ (incf total) (incf wolf))
                     (#.+sheep+ (incf total) (incf sheep))))))))
         (results (apply #'mapcar #'+ results)))
    (destructuring-bind (total sheep wolf unknown) results
      (format t "~&total valid: ~a, invalid: ~a~%" total (- (max-state max-n-piece) total))
      (format t "sheep: ~a (~,2f%)~%" sheep (/ (* 100 sheep) total))
      (format t "wolf: ~a (~,2f%)~%" wolf (/ (* 100 wolf) total))
      (format t "unknown: ~a (~,2f%)~%" unknown (/ (* 100 unknown) total)))))

(defun tablebase-summary-1 (tablebase n-piece)
  (check-type tablebase tablebase)
  (sb-thread:barrier (:memory))
  (let* ((max-wolf-index (max-wolf-index n-piece))
         (results
           (do-states (state n-piece)
               ((total 0)
                (sheep 0)
                (wolf 0)
                (unknown 0))
             (declare (optimize speed)
                      (type tablebase tablebase)
                      (type bitpos n-piece)
                      (type (unsigned-byte 10) max-wolf-index)
                      (type fixnum total sheep wolf unknown))
             (when (= (logcount (ldb (byte 25 0) state)) n-piece)
               (when (<= (ldb (byte 10 25) state) max-wolf-index)
                 (case (bref tablebase state)
                   (#.+unknown+ (incf total) (incf unknown))
                   (#.+wolf+ (incf total) (incf wolf))
                   (#.+sheep+ (incf total) (incf sheep)))))))
         (results (apply #'mapcar #'+ results)))
    (destructuring-bind (total sheep wolf unknown) results
      (format t "~&total valid: ~a~%" total)
      (format t "sheep: ~a (~,2f%)~%" sheep (/ (* 100 sheep) total))
      (format t "wolf: ~a (~,2f%)~%" wolf (/ (* 100 wolf) total))
      (format t "unknown: ~a (~,2f%)~%" unknown (/ (* 100 unknown) total))
      unknown)))

(defun wolf-forward-scan (pair n-piece)
  (sb-thread:barrier (:memory))
  (let ((sheep (car pair))
        (wolf (cdr pair))
        (max-wolf-index (max-wolf-index n-piece)))
    (declare (type tablebase sheep wolf)
             (type bitpos n-piece)
             (type (unsigned-byte 10) max-wolf-index))
    (format t "~&Scan, wolf move, ~a pieces.~%" n-piece)
    (do-states (state n-piece) ()
      (declare (optimize speed))
      (when (= (logcount (ldb (byte 25 0) state)) n-piece)
        (when (<= (ldb (byte 10 25) state) max-wolf-index)
          (when (= (bref wolf state) +unknown+)
            (let ((fate +sheep+))
              (declare (type fate fate))
              (wolf-forward state
                            (lambda (next-state)
                              (declare (type state next-state))
                              (let ((next-fate (bref sheep next-state)))
                                (setq fate (logxor (max (logxor fate 2) (logxor next-fate 2)) 2)))))
              (setf (bref wolf state) fate))))))))

(defun sheep-forward-scan (pair n-piece)
  (sb-thread:barrier (:memory))
  (let ((sheep (car pair))
        (wolf (cdr pair))
        (max-wolf-index (max-wolf-index n-piece)))
    (declare (type tablebase sheep wolf)
             (type bitpos n-piece)
             (type (unsigned-byte 10) max-wolf-index))
    (format t "~&Scan, sheep move, ~a pieces.~%" n-piece)
    (let ((results
            (do-states (state n-piece) ((delta 0))
              (declare (optimize speed) (fixnum delta))
              (when (= (logcount (ldb (byte 25 0) state)) n-piece)
                (when (<= (ldb (byte 10 25) state) max-wolf-index)
                  (when (= (bref sheep state) +unknown+)
                    (let ((fate +wolf+))
                      (declare (type fate fate))
                      (sheep-forward state
                                     (lambda (next-state)
                                       (declare (type state next-state))
                                       (let ((next-fate (bref wolf next-state)))
                                         (setq fate (logxor (max (logxor fate 1) (logxor next-fate 1)) 1)))))
                      (when (plusp fate) (incf delta))
                      (setf (bref sheep state) fate))))))))
      (reduce #'+ results :key #'car))))

(defun wolf-forward-scan-create-worklist (pair n-piece)
  (sb-thread:barrier (:memory))
  (let ((sheep (car pair))
        (wolf (cdr pair))
        (max-wolf-index (max-wolf-index n-piece)))
    (declare (type tablebase sheep wolf)
             (type bitpos n-piece)
             (type (unsigned-byte 10) max-wolf-index))
    (format t "~&Scan create worklist, wolf move, ~a pieces.~%" n-piece)
    (let ((results
            (do-states (state n-piece) ((worklist))
              (declare (optimize speed))
              (when (= (logcount (ldb (byte 25 0) state)) n-piece)
                (when (<= (ldb (byte 10 25) state) max-wolf-index)
                  (when (= (bref wolf state) +unknown+)
                    (let ((fate +sheep+))
                      (declare (type fate fate))
                      (wolf-forward state
                                    (lambda (next-state)
                                      (declare (type state next-state))
                                      (let ((next-fate (bref sheep next-state)))
                                        (setq fate (logxor (max (logxor fate 2) (logxor next-fate 2)) 2)))))
                      (when (plusp fate)
                        (sheep-backward state
                                        (lambda (prev-state)
                                          (push prev-state worklist))))
                      (setf (bref wolf state) fate))))))))
      (sort-worklist (mapcan #'car results)))))

(defun sort-worklist (worklist)
  (setq worklist (sort worklist #'<))
  (do ((tail worklist))
      ((null (cdr tail)))
    (if (= (car tail) (cadr tail))
        (rplacd tail (cddr tail))
        (setq tail (cdr tail))))
  worklist)

(defun chop-worklist (worklist n)
  (let ((m (min 1000 (ceiling (length worklist) n)))
        (results))
    (do ((tail (cons nil worklist))
         (i 0 (1+ i)))
        ((null tail) results)
      (if (zerop (mod i m))
          (let ((next (cdr tail)))
            (push next results)
            (rplacd tail nil)
            (setq tail next))
          (setq tail (cdr tail))))))

(defmacro do-worklist ((state-var worklist) (&rest bindings) &body body)
  `(let ((results nil)
         (works (remove-if #'null (chop-worklist ,worklist 256)))
         (channel (lparallel:make-channel)))
     (dolist (w works)
       (lparallel:submit-task
        channel
        (lambda (%worklist)
          (let ,bindings
            (dolist (,state-var %worklist)
              (declare (state ,state-var))
              ,@body)
            (or (list ,@(mapcar #'car bindings)) t)))
        w))
     (unwind-protect
          (iter (for last-progress = (length results))
            (while (< last-progress (length works)))
            (sleep .2)
            (iter (for r = (lparallel:try-receive-result channel))
              (while r)
              (push r results))
            (unless (= last-progress (length results))
              (format t "~,2f% " (/ (* 100 (length results)) (length works))))
            (finally (return results)))
       (lparallel:kill-tasks :default))))

(defun sheep-forward-scan-use-worklist (pair worklist)
  (sb-thread:barrier (:memory))
  (let ((sheep (car pair))
        (wolf (cdr pair)))
    (declare (type tablebase sheep wolf))
    (format t "~&Worklist, sheep move.~%")
    (let ((results
            (do-worklist (state worklist) ((worklist))
              (declare (optimize speed))
              (when (= (bref sheep state) +unknown+)
                (let ((fate +wolf+))
                  (declare (type fate fate))
                  (sheep-forward state
                                 (lambda (next-state)
                                   (declare (type state next-state))
                                   (let ((next-fate (bref wolf next-state)))
                                     (setq fate (logxor (max (logxor fate 1) (logxor next-fate 1)) 1)))))
                  (when (plusp fate)
                    (wolf-move-backward state
                                        (lambda (prev-state)
                                          (push prev-state worklist))))
                  (setf (bref sheep state) fate))))))
      (sort-worklist (mapcan #'car results)))))

(defun wolf-forward-scan-use-worklist (pair worklist)
  (sb-thread:barrier (:memory))
  (let ((sheep (car pair))
        (wolf (cdr pair)))
    (declare (type tablebase sheep wolf))
    (format t "~&Worklist, wolf move.~%")
    (let ((results
            (do-worklist (state worklist) ((worklist))
              (declare (optimize speed))
              (let ((fate +sheep+))
                (declare (type fate fate))
                (wolf-forward state
                              (lambda (next-state)
                                (declare (type state next-state))
                                (let ((next-fate (bref sheep next-state)))
                                  (setq fate (logxor (max (logxor fate 2) (logxor next-fate 2)) 2)))))
                (when (plusp fate)
                  (sheep-backward state
                                  (lambda (prev-state)
                                    (push prev-state worklist))))
                (setf (bref wolf state) fate)))))
      (sort-worklist (mapcan #'car results)))))

(defvar *endgames*)

(defun run (n-iter n-piece)
  (assert (<= 7 n-piece 17))
  (iter (repeat n-iter)
    (wolf-forward-scan *endgames* n-piece)
    (let ((delta (sheep-forward-scan *endgames* n-piece)))
      (tablebase-summary-1 (car *endgames*) n-piece)
      (format t "~&state change: ~a~%" delta)
      (cond ((zerop delta)
             (format t "~&===Done===~%")
             (return-from run))
            ((< delta 10000000)
             (format t "~&===Switch to worklist===~%")
             (format t "use ~a worklist iterations~%" (* n-iter 10))
             (run-worklist (* n-iter 10) n-piece)
             (return-from run))))))

(defun run-worklist (n-iter n-piece)
  (assert (<= 7 n-piece 17))
  (iter (with processed = 0)
    (for i below n-iter)
    (for worklist
         initially (wolf-forward-scan-create-worklist *endgames* n-piece)
         then (wolf-forward-scan-use-worklist *endgames* worklist))
    (while worklist)
    (setq worklist (sheep-forward-scan-use-worklist *endgames* worklist))
    (let ((s (length worklist)))
      (format t "~&worklist size: ~a~%" s)
      (when (> (floor (+ s processed) 1000000)
               (floor processed 1000000))
        (tablebase-summary-1 (car *endgames*) n-piece)))
    (while worklist))
  (format t "~&===Done===~%"))

(defun load-endgames (sheep-file wolf-file)
  (unless (boundp '*endgames*)
    (setq *endgames*
          (cons (static-vectors:make-static-vector (ash (max-state 17) -2) :initial-element 0)
                (static-vectors:make-static-vector (ash (max-state 17) -2) :initial-element 0))))
  (with-open-file (in sheep-file :element-type '(unsigned-byte 8))
    (read-sequence (car *endgames*) in))
  (with-open-file (in wolf-file :element-type '(unsigned-byte 8))
    (read-sequence (cdr *endgames*) in)))

(defun save-endgames (sheep-file wolf-file)
  (with-open-file (out sheep-file :direction :output :element-type '(unsigned-byte 8))
    (write-sequence (car *endgames*) out))
  (with-open-file (out wolf-file :direction :output :element-type '(unsigned-byte 8))
    (write-sequence (cdr *endgames*) out)))

(defun unload-endgames ()
  (let ((e *endgames*))
    (makunbound '*endgames*)
    (static-vectors:free-static-vector (car e))
    (setf (car e) nil)
    (static-vectors:free-static-vector (cdr e))
    (setf (cdr e) nil)))

#+nil (progn
        (static-vectors:free-static-vector (car *endgames*))
        (static-vectors:free-static-vector (cdr *endgames*))
        (makunbound '*endgames*))

#+nil (with-open-file (out "~/playground/wolf-chess-init.bin" :direction :output :element-type '(unsigned-byte 8))
        (write-sequence (car *endgames*) out))


#+nil (load-endgames "~/playground/wolf-chess-10s.bin" "~/playground/wolf-chess-10w.bin")

#+nil (progn
  (with-open-file (out "~/playground/wolf-chess-10s.bin" :direction :output :element-type '(unsigned-byte 8))
    (write-sequence (car *endgames*) out))
  (with-open-file (out "~/playground/wolf-chess-10w.bin" :direction :output :element-type '(unsigned-byte 8))
    (write-sequence (cdr *endgames*) out)))

#+nil (init-tablebase *endgames*)
